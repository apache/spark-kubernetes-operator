/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */
dependencies {
  // fabric8
  implementation("io.fabric8:kubernetes-client:$fabric8Version") {
    exclude group: 'com.squareup.okhttp3'
  }
  compileOnly("io.fabric8:crd-generator-apt:$fabric8Version")
  annotationProcessor("io.fabric8:crd-generator-apt:$fabric8Version")

  // utils
  implementation("commons-io:commons-io:$commonsIOVersion")
  implementation("org.projectlombok:lombok:$lombokVersion")
  annotationProcessor("org.projectlombok:lombok:$lombokVersion")

  // logging
  implementation("org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion")
  implementation("org.apache.logging.log4j:log4j-core:$log4jVersion")

  testImplementation platform("org.junit:junit-bom:$junitVersion")
  testImplementation 'org.junit.jupiter:junit-jupiter'
  testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

test {
  useJUnitPlatform()
}

// Adds additional printer columns to generated yaml
// This requires yq
tasks.register('finalizeGeneratedCRD', Exec) {
  dependsOn jar
  println "Updating PrinterColumns for generated CRD"
  commandLine 'sh', './src/main/resources/printer-columns.sh'
}

// Copy generated yaml to Helm charts
tasks.register('relocateGeneratedCRD', Copy) {
  dependsOn finalizeGeneratedCRD
  from "build/classes/java/main/META-INF/fabric8/sparkapplications.spark.apache.org-v1.yml"
  into "../build-tools/helm/spark-kubernetes-operator/crds"
  rename '(.+).yml', '$1.yaml'
}
