/*
 * Licensed to the Apache Software Foundation (ASF) under one
 * or more contributor license agreements.  See the NOTICE file
 * distributed with this work for additional information
 * regarding copyright ownership.  The ASF licenses this file
 * to you under the Apache License, Version 2.0 (the
 * "License"); you may not use this file except in compliance
 * with the License.  You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied.  See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

ext {
    javaMainClass = "org.apache.spark.k8s.operator.utils.ConfOptionDocGenerator"
    defaultDocsPath = "${project.rootDir}/docs"
    docOutputPath = project.hasProperty("overwrite") ? defaultDocsPath : "build"
    generatedDocFileName = "config_properties.md"
}

dependencies {
    implementation project(":spark-operator")
    implementation(libs.log4j.core)
    implementation(libs.log4j.slf4j2.impl)
    compileOnly(libs.lombok)
    annotationProcessor(libs.lombok)
}

test {
    useJUnitPlatform()
}

tasks.register('generateConfPropsDoc', Exec) {
    description = "Generate config properties doc for operator"
    dependsOn jar
    commandLine "java", "-classpath", sourceSets.main.runtimeClasspath.getAsPath(), javaMainClass, docOutputPath
    println("Doc generated at $docOutputPath/$generatedDocFileName")
}

tasks.register('validateConfPropsDocUpToDate') {
    description = "Validate the generate config properties doc is up to date"
    dependsOn 'generateConfPropsDoc'

    doLast {
      File generated = file("$docOutputPath/$generatedDocFileName")
      File staged = file("$defaultDocsPath/$generatedDocFileName")
      if (!staged.exists()) {
        throw new GradleException("Staged properties doc file does not exist.")
      }
      String generatedText = generated.text.trim()
      String stagedText = staged.text.trim()
      if (generatedText != stagedText) {
        throw new GradleException("Doc $defaultDocsPath/$generatedDocFileName out of date: " +
          "if operator conf properties have been updated, please re-generate properties docs " +
          "by running ./gradlew build-tools-docs-utils:generateConfPropsDoc -Poverwrite")
      }
    }
}

build.finalizedBy(validateConfPropsDocUpToDate)
